plugins {
    id 'biz.aQute.bnd.builder' version '5.3.0' apply false
    id 'jacoco'
    id 'maven-publish'
    id 'signing'
}

// Note: The following version number is updated using the set-version.sh tool.
// It is used as the version number of the managers bundle, which contains a yaml
// file which is in a release.yaml, but published to maven, so that the OBR build 
// can pick it up later.
version = "0.31.0"

// A configuration to publish the merge exec into
configurations {
    archives
    release_metadata
}

//  Get all the sub projects with jacoco plugin to add itself to the merge 
subprojects {
   plugins.withId('jacoco')  {
        rootProject.tasks.named('jacocoMerge').get().executionData(tasks.named('test').get())
   }
}

// Define the artifact
def execFile = layout.buildDirectory.file('jacoco/jacocoMerge.exec')
def execArtifact = artifacts.add('archives', execFile.get().asFile) {
    builtBy 'jacocoMerge'
}


task jacocoMerge(type: JacocoMerge) {
    doFirst {
        // go through all the files and remove the ones that do not exist.   some managers do not have unit tests yet
        executionData = executionData.filter({f -> f.exists()})
    }

    enabled = jacocoEnabled.toBoolean()
}

repositories {
    maven {
        url "$sourceMaven"
    }
    maven {
        url "$centralMaven"
    }
}

signing {
    sign publishing.publications
}

tasks.withType(Sign) {
    onlyIf { isMainOrRelease.toBoolean() }
}

if (jacocoEnabled.toBoolean()) {
    publishing {
        publications {
            maven(MavenPublication) {
                artifact execArtifact
                
                groupId = 'codecoverage'
                artifactId = 'manager-unit-tests'
            }
        }
        repositories {
            maven {
                url  = "$targetMaven"
                
                if ("$targetMaven".startsWith('http')) {
                    credentials {
                        username System.getenv('MAVENUSERNAME')
                        password System.getenv('MAVENPASSWORD')
                    }
                }
            }
        }
    }
}


def manifestFilePath = "$buildDir/release.yaml"

def header = """#
# Copyright contributors to the Galasa project 
#

# -----------------------------------------------------------
#
#                         WARNING
#
# This file is periodically re-generated from the contents of 
# the repository, so don't make changes here manually please.
# -----------------------------------------------------------


apiVersion: galasa.dev/v1alpha
kind: Release
metadata:
  name: galasa-release
    
managers:
  bundles:

#
# Manager 
#
"""

// Build the release.yaml file
task buildReleaseYaml() {
    println 'Building the release.yaml file...'

    // During execution phase, make sure the file exists.
    doFirst{
        if ( !buildDir.exists() ) {
            buildDir.mkdirs()
        }
        def manifest_file = new File(manifestFilePath)
        if (!manifest_file.exists()){
            manifest_file.createNewFile()
        } else {
            manifest_file.delete()
            manifest_file.createNewFile()
        }
        manifest_file.append(header)
    }


    subprojects {
        ext {
            // the property that should be overridden in suproject's build.gradle
            // Each sub-project will set the values...
            projectName = ''
            includeInOBR = ''
            includeInMVP = ''
            includeInBOM = ''
            includeInJavadoc = ''
            includeInIsolated = ''
            includeInCodeCoverage = ''
        }

        afterEvaluate {
            doLast {
                // Some projects don't have a version property... as they are parent projects mostly.
                if (version != 'unspecified') {
                    def f = new File(manifestFilePath)
                    f.append("\n\n  - artifact: $projectName")
                    f.append("\n    version: $version")
                    if (includeInOBR != '') {
                        f.append("\n    obr:          $includeInOBR")
                    }
                    if (includeInMVP != '') {
                        f.append("\n    mvp:          $includeInMVP")
                    }
                    if (includeInBOM != '') {
                        f.append("\n    bom:          $includeInBOM")
                    }
                    if (includeInJavadoc != '') {
                        f.append("\n    javadoc:      $includeInJavadoc")
                    }
                    if (includeInIsolated != '') {
                        f.append("\n    isolated:     $includeInIsolated")
                    }
                    if (includeInCodeCoverage != '') {
                        f.append("\n    codecoverage: $includeInCodeCoverage")
                    }
                }
            }
        }
    }
}

def myReleaseYaml = artifacts.add('release_metadata', file("$buildDir/release.yaml")) {
    builtBy 'buildReleaseYaml'
}

// Publish the release.yaml as a maven artifact.
// Note: The maven co-ordinates are versioned using the version for this bundle.
publishing {
    publications {

        // Publish the component manifest/release.yaml
        publishReleaseManifestYaml(MavenPublication) {
            artifact myReleaseYaml

            pom {
                name = "Manifest for managers bundle versions"
                artifactId = "dev.galasa.managers.manifest"
                groupId = 'dev.galasa'
				version = "0.31.0"
                description = "Conveys bundle version information to OBR builds."
                licenses {
                    license {
                        name = 'Eclipse Public License - v 2.0'
                        url = 'https://www.eclipse.org/legal/epl-2.0/t'
                    }
                }
                url = 'https://galasa.dev'
                developers {
                    developer {
                        name = 'Galasa Developer'
                        email = 'galasadelivery@ibm.com'
                        organization = 'IBM'
                        organizationUrl = 'https://www.ibm.com'
                    }
                }
                scm {
                    connection = 'scm:git:git:://github.com/galasa-dev/managers'
                    developerConnection = 'scm:git:git:://github.com/galasa-dev/managers'
                    url = 'https://github.com/galasa-dev/managers'
                }
                issueManagement {
                    system = 'GitHub'
                    url = 'https://github.com/galasa-dev/projectmanagement/issues'
                }
            }

        }
    }

    repositories {
        maven {
            url  = "$targetMaven"
        
            if ("$targetMaven".startsWith('http')) {
                credentials {
                    username System.getenv('MAVENUSERNAME')
                    password System.getenv('MAVENPASSWORD')
                }
            }
        }
    }
}


